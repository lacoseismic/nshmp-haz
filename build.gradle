/*
 * In order to build and run services locally,
 * one needs to include the following directorie(s) in the root project (they
 * are ignored by git):
 *
 *    models/
 *       ak/
 *          2007/
 *       wus/
 *          2008/
 *          2014/
 *          2014b/
 *          2018/
 *       ceus/
 *          2008/
 *          2014/
 *          2018/
 *
 * ...with each 'year' directory being an alias to the corresponding
 * git repository. One only needs to include the model they will run in the services.
 *
 * Then build the JAR file: ./gradlew assemble
 *
 * One can then specify the model to run in the services:
 *    java -jar build/libs/nshmp-haz.jar --models=/path/to/models
 *
 * Where <MODEL> is one of the model enums in nshmp.www.Model
 */

plugins {
  id "application"
  id "com.diffplug.gradle.spotless" version "${spotlessVersion}"
  id "com.github.johnrengelman.shadow" version "${shadowVersion}"
  id "com.github.node-gradle.node" version "${nodeVersion}"
  id "com.github.spotbugs" version "${spotbugsVersion}"
  id "com.star-zero.gradle.githook" version "${githooksVersion}"
  id "eclipse-wtp"
  id "io.micronaut.application" version "${mnPluginVersion}"
  id "jacoco"
  id "maven-publish"
}

configurations {
  nshmp
}

apply from: "${projectDir}/gradle/dependencies.gradle"
apply from: "${projectDir}/gradle/ext.gradle"
apply from: "${projectDir}/gradle/jar.gradle"
apply from: "${projectDir}/gradle/javadoc.gradle"
apply from: "${projectDir}/gradle/repositories.gradle"

sourceCompatibility = JavaVersion.VERSION_11
compileJava.options.encoding = "UTF-8"

mainClassName = "gov.usgs.earthquake.nshmp.www.Application"

jacoco {
  toolVersion = "0.8.4"
}

test {
  useJUnitPlatform()
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled true
  }
  afterEvaluate {
    classDirectories.from(files(classDirectories.files.collect {
      fileTree(
          dir: it,
          exclude: ["**/etc/**"])
    }))
  }
}
check.dependsOn jacocoTestReport

tasks.withType(JavaCompile) {
  options.encoding = "UTF-8"
  options.compilerArgs.add("-parameters")
}

tasks.withType(JavaExec) {
  jvmArgs('-noverify', '-Xms2g', '-Xmx8g', '-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')
}

/* Add HTML reports to SpotBugs */
tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
  reports {
    html {
      enabled true
      stylesheet = 'fancy-hist.xsl'
    }
  }
}

task libsClean(type: Delete) {
  delete libsDir
}
clean.dependsOn libsClean

gradle.afterProject {
  copy {
    from {
      configurations.nshmp.collect { zipTree(it) }
    }
    into nshmpLib
  }
  apply from: "${nshmpLibGradleDir}/git-hooks.gradle"
  apply from: "${nshmpLibGradleDir}/node.gradle"
  apply from: "${nshmpLibGradleDir}/spotbugs.gradle"
  apply from: "${nshmpLibGradleDir}/spotless.gradle"
}
